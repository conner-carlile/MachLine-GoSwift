! Subroutines for I/O with VTK files
module vtk_mod

    use panel_mod
    use vertex_mod

    implicit none

    
contains

    subroutine load_surface_vtk(mesh_file, N_verts, N_panels, vertices, panels)
        ! Loads a surface mesh from a vtk file. Only a body.
        ! Needs to be updated to automatically delete duplicate vertices.

        implicit none

        character(len=:),allocatable,intent(in) :: mesh_file
        integer,intent(out) :: N_verts, N_panels
        character(len=200) :: dummy_read
        type(vertex),dimension(:),allocatable,intent(inout) :: vertices
        type(panel),dimension(:),allocatable,intent(inout) :: panels
        real,dimension(3) :: vertex_loc
        integer :: i, j, N, i1, i2, i3, i4

        ! Open file
        open(1, file=mesh_file)

            ! Determine number of vertices
            read(1,*) ! Header
            read(1,*) ! Header
            read(1,*) ! Header
            read(1,*) ! Header
            read(1,*) dummy_read, N_verts, dummy_read

            ! Allocate vertex array
            allocate(vertices(N_verts))

            ! Store vertices
            do i=1,N_verts

                ! Read in from file
                read(1,*) vertex_loc(1), vertex_loc(2), vertex_loc(3)

                ! Initialize
                call vertices(i)%init(vertex_loc, i)

            end do

            ! Determine number of panels
            read(1,*) dummy_read, N_panels, dummy_read

            ! Allocate panel array
            allocate(panels(N_panels))

            ! Initialize panels
            do i=1,N_panels

                ! Get data
                read(1,'(a)') dummy_read
                
                ! Determine size of panel
                if (dummy_read(1:2) == '3 ') then
                    read(dummy_read,*) N, i1, i2, i3
                else
                    write(*,*) "MFTran supports only triangular panels."
                    stop
                end if

                ! Initialize triangular panel
                if (N == 3) then
                    call panels(i)%init(vertices(i1+1), vertices(i2+1), vertices(i3+1),&
                                        i1+1, i2+1, i3+1, i) ! Need +1 because VTK uses 0-based indexing

                    ! Add panel index to vertices
                    call vertices(i1+1)%panels%append(i)
                    call vertices(i2+1)%panels%append(i)
                    call vertices(i3+1)%panels%append(i)
                end if

            end do

        close(1)
    
    end subroutine load_surface_vtk


    subroutine write_surface_vtk(output_file, vertices, panels, sigma, mu, is_wake, vel, C_p)

        implicit none

        character(len=:),allocatable,intent(in) :: output_file
        type(vertex),dimension(:),intent(in) :: vertices
        type(panel),dimension(:),intent(in) :: panels
        real,dimension(:),allocatable,optional,intent(in) :: sigma, mu, C_p
        real,dimension(:,:),allocatable,optional,intent(in) :: vel
        logical,optional :: is_wake
        integer :: i, N_verts, N_panels, panel_info_size, j

        ! Open file
        open(1, file=output_file)

            ! Write header
            write(1,'(a)') "# vtk DataFile Version 3.0"
            write(1,'(a)') "MFTran results file. Generated by MFTran, USU AeroLab (c) 2021."
            write(1,'(a)') "ASCII"

            ! Write vertex information
            N_verts = size(vertices)
            write(1,'(a)') "DATASET POLYDATA"
            write(1,'(a i20 a)') "POINTS", N_verts, " float"

            ! Write out vertices
            100 format(f20.12, ' ', f20.12, ' ', f20.12) ! Vertices
            do i=1,N_verts
                write(1,100) vertices(i)%loc(1), vertices(i)%loc(2), vertices(i)%loc(3)
            end do

            ! Determine panel info size
            panel_info_size = 0
            N_panels = size(panels)
            do i=1,N_panels
                panel_info_size = panel_info_size + panels(i)%N + 1
            end do
            
            ! Write out panels
            write(1,'(a i20 i20)') "POLYGONS", N_panels, panel_info_size
            do i=1,N_panels

                ! Number of vertices
                write(1,'(i1) ',advance='no') panels(i)%N

                ! Indices of each vertex
                do j=1,panels(i)%N
                    write(1,'(i20) ',advance='no') panels(i)%vertices(j)%ptr%index-1
                end do
                write(1,*)

            end do

            ! Write out panel normals
            write(1,'(a i20)') "CELL_DATA", N_panels
            write(1,'(a)') "NORMALS panel_normals float"
            do i=1,N_panels
                write(1,100) panels(i)%normal(1), panels(i)%normal(2), panels(i)%normal(3)
            end do

            ! Indices
            write(1,'(a)') "SCALARS index integer 1"
            write(1,'(a)') "LOOKUP_TABLE default"
            do i=1,N_panels
                write(1,'(i20)') i
            end do

            ! Check if this is a wake mesh
            if (present(is_wake) .and. is_wake) then

                ! Panel source strengths
                if (present(sigma)) then
                    write(1,'(a)') "SCALARS sigma float 1"
                    write(1,'(a)') "LOOKUP_TABLE default"
                    do i=1,N_panels
                        write(1,'(f20.12)') 0.
                    end do
                end if

                ! Vertex doublet strengths
                if (present(mu)) then
                    write(1, '(a i20)') "POINT_DATA", N_verts
                    write(1,'(a)') "SCALARS mu float 1"
                    write(1,'(a)') "LOOKUP_TABLE default"
                    do i=1,N_verts
                        write(1,'(f20.12)') mu(vertices(i)%top_parent)-mu(vertices(i)%bot_parent)
                    end do
                end if

            ! Regular body mesh
            else

                ! Panel source strengths
                if (present(sigma)) then
                    write(1,'(a)') "SCALARS sigma float 1"
                    write(1,'(a)') "LOOKUP_TABLE default"
                    do i=1,N_panels
                        write(1,'(f20.12)') sigma(i)
                    end do
                end if

                ! Panel velocities
                if (present(vel)) then
                    write(1,'(a)') "VECTORS V float"
                    do i=1,N_panels
                        write(1,100) vel(i,1), vel(i,2), vel(i,3)
                    end do
                end if

                ! Panel pressure coefficients
                if (present(C_p)) then
                    write(1,'(a)') "SCALARS C_p float 1"
                    write(1,'(a)') "LOOKUP_TABLE default"
                    do i=1,N_panels
                        write(1,'(f20.12)') C_p(i)
                    end do
                end if

                ! Vertex doublet strengths
                if (present(mu)) then
                    write(1, '(a i20)') "POINT_DATA", N_verts
                    write(1,'(a)') "SCALARS mu float 1"
                    write(1,'(a)') "LOOKUP_TABLE default"
                    do i=1,N_verts
                        write(1,'(f20.12)') mu(i)
                    end do
                end if

            end if

        close(1)
    
    end subroutine write_surface_vtk


    subroutine write_point_vtk(output_file, points, phi, phi_mu, phi_sigma)

        implicit none

        character(len=:),allocatable,intent(in) :: output_file
        real,dimension(:,:),intent(in) :: points
        real,dimension(:),intent(in) :: phi, phi_mu, phi_sigma
        integer :: i, N_verts, vertex_info_size

        ! Open file
        open(1, file=output_file)

            ! Write header
            write(1,'(a)') "# vtk DataFile Version 3.0"
            write(1,'(a)') "MFTran results file. Generated by MFTran, USU AeroLab (c) 2021."
            write(1,'(a)') "ASCII"

            ! Write point information
            N_verts = size(points)/3
            write(1,'(a)') "DATASET POLYDATA"
            write(1,'(a i20 a)') "POINTS", N_verts, " float"

            ! Write out points
            100 format(f20.12, ' ', f20.12, ' ', f20.12) ! Vertices
            do i=1,N_verts
                write(1,100) points(i,1), points(i,2), points(i,3)
            end do

            ! Determine vertex info size
            vertex_info_size = 2*N_verts
            
            ! Write out panels
            write(1,'(a i20 i20)') "VERTICES", N_verts, vertex_info_size
            do i=1,N_verts

                ! Number of vertices
                write(1,'(i1) ',advance='no') 1

                ! Index of each vertex
                write(1,'(i20)') i-1

            end do

            ! Potential at points
            write(1, '(a i20)') "POINT_DATA", N_verts
            write(1,'(a)') "SCALARS phi float 1"
            write(1,'(a)') "LOOKUP_TABLE default"
            do i=1,N_verts
                write(1,'(f20.12)') phi(i)
            end do

            ! Potential at points due to doublet distribution
            write(1,'(a)') "SCALARS phi_mu float 1"
            write(1,'(a)') "LOOKUP_TABLE default"
            do i=1,N_verts
                write(1,'(f20.12)') phi_mu(i)
            end do

            ! Potential at points due to source distribution
            write(1,'(a)') "SCALARS phi_sigma float 1"
            write(1,'(a)') "LOOKUP_TABLE default"
            do i=1,N_verts
                write(1,'(f20.12)') phi_sigma(i)
            end do

            ! Indices
            write(1,'(a)') "SCALARS index integer 1"
            write(1,'(a)') "LOOKUP_TABLE default"
            do i=1,N_verts
                write(1,'(i20)') i
            end do

        close(1)
        
    
    end subroutine write_point_vtk

    
end module vtk_mod